%%[

/* Declare and initialize */
Var @rows, @rowCount, @i, @row
Var @hotelName, @hotelCity, @checkIn, @checkOut, @UniqueID
Var @paymentStatus, @GuestCommsType, @ConfirmedDate
Var @hotelOutput, @validBookingCount, @SubscriberKey, @processedKeys, @currentKey

SET @SubscriberKey = AttributeValue("subscriberKey")
SET @rows = LookupOrderedRows("ENT.OLY_MC26_Booking_Items", 0, "PackageStartTime ASC", "subscriberKey", @SubscriberKey)
SET @rowCount = RowCount(@rows)

SET @hotelOutput = ""
SET @validBookingCount = 0
SET @processedKeys = ""

IF @rowCount > 0 THEN

  FOR @i = 1 TO @rowCount DO

    SET @row = Row(@rows, @i)
    SET @hotelName = Field(@row, "HotelName")
    SET @paymentStatus = Field(@row, "PaymentStatus")
    SET @GuestCommsType = Field(@row, "GuestCommsType")
    SET @ConfirmedDate = Field(@row, "ConfirmedDate")
    SET @checkIn = Field(@row, "PackageStartTime")
    SET @checkOut = Field(@row, "PackageEndTime")

    IF NOT EMPTY(@hotelName) AND 
       @GuestCommsType == "All Comms" AND 
       NOT EMPTY(@ConfirmedDate) THEN

      SET @currentKey = CONCAT(@hotelName,"|", Format(@checkIn, "yyyy-MM-dd"), "|", Format(@checkOut, "yyyy-MM-dd"))

      IF IndexOf(@processedKeys, @currentKey) == 0 THEN

        SET @hotelCity = Field(@row, "VenueCluster")
        SET @UniqueID = Field(@row, "UniqueID")

        IF @validBookingCount == 0 THEN
          SET @hotelOutput = "<p>Just to remind you, here's your hotel information:</p><ul style='margin: 0; padding-left: 20px;'>"
        ENDIF

        SET @hotelOutput = CONCAT(@hotelOutput,
          "<li>",
          @hotelName,
          IIF(EMPTY(@hotelCity), "", CONCAT(" (", @hotelCity, ")")),
          ": ",
          Format(@checkIn, "MMMM d, yyyy"),
          " - ",
          Format(@checkOut, "MMMM d, yyyy"),
          "</li>"
        )

        SET @processedKeys = CONCAT(@processedKeys, @currentKey, ",")
        SET @validBookingCount = ADD(@validBookingCount, 1)

      ENDIF

    ENDIF

  NEXT @i

  IF @validBookingCount > 0 THEN
    SET @hotelOutput = CONCAT(@hotelOutput, "</ul>")
  ENDIF

ENDIF

]%%
